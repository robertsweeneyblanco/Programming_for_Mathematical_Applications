{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Graph Code\n",
    "\n",
    "This notebook contains all the code for creating and plotting graphs from the *Graph_Basics* notebook."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "using PyPlot \n",
    "\n",
    "struct Vertex\n",
    "    neighbors::Vector{Int}       # Indices of neighbors of this Vertex \n",
    "    coordinates::Vector{Float64} # 2D coordinates of this Vertex - only for plotting\n",
    "    Vertex(neighbors; coordinates=[0,0]) = new(neighbors, coordinates)\n",
    "end\n",
    "\n",
    "function Base.show(io::IO, v::Vertex)\n",
    "    print(io, \"Neighbors = \", v.neighbors)\n",
    "end\n",
    "\n",
    "struct Graph\n",
    "    vertices::Vector{Vertex}\n",
    "end\n",
    "\n",
    "function Base.show(io::IO, g::Graph)\n",
    "    for i = 1:length(g.vertices)\n",
    "        println(io, \"Vertex $i, \", g.vertices[i])\n",
    "    end\n",
    "end\n",
    "\n",
    "function PyPlot.plot(g::Graph; scale=1.0)\n",
    "    fig, ax = subplots()\n",
    "    ax.set_aspect(\"equal\")\n",
    "    \n",
    "    xmin = minimum(v.coordinates[1] for v in g.vertices)\n",
    "    xmax = maximum(v.coordinates[1] for v in g.vertices)\n",
    "    ymin = minimum(v.coordinates[2] for v in g.vertices)\n",
    "    ymax = maximum(v.coordinates[2] for v in g.vertices)\n",
    "    sz = max(xmax-xmin, ymax-ymin)\n",
    "    cr = scale*0.05sz\n",
    "    hw = cr/2\n",
    "    axis([xmin-2cr,xmax+2cr,ymin-2cr,ymax+2cr])\n",
    "    axis(\"off\")\n",
    "\n",
    "    for i in 1:length(g.vertices)\n",
    "        c = g.vertices[i].coordinates\n",
    "        ax.add_artist(matplotlib.patches.Circle(c, cr, facecolor=\"none\", edgecolor=\"k\"))\n",
    "        ax.text(c[1], c[2], string(i),\n",
    "                horizontalalignment=\"center\", verticalalignment=\"center\", fontsize=round(Int, 14*scale))\n",
    "        for nb in g.vertices[i].neighbors\n",
    "            cnb = g.vertices[nb].coordinates\n",
    "            dc = cnb .- c\n",
    "            L = sqrt(sum(dc.^2))\n",
    "            c1 = c .+ cr/L * dc\n",
    "            c2 = cnb .- cr/L * dc\n",
    "            arrow(c1[1], c1[2], c2[1]-c1[1], c2[2]-c1[2],\n",
    "                  head_width=hw, length_includes_head=true, facecolor=\"k\")\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "function circle_graph(nv=8)\n",
    "    g = Graph([])\n",
    "    for i = 1:nv\n",
    "        th = 2Ï€*i/nv\n",
    "        v = Vertex([mod(i,nv)+1, mod(i-2,nv)+1], coordinates=[cos(th), sin(th)])\n",
    "        push!(g.vertices, v)\n",
    "    end\n",
    "    return g\n",
    "end\n",
    "\n",
    "\n",
    "g = circle_graph(10)\n",
    "\n",
    "# Add edges by growing the neighbors array\n",
    "push!(g.vertices[1].neighbors, 6);       # Add edge 1->6\n",
    "append!(g.vertices[4].neighbors, [2,7]); # Add 2 new edges\n",
    "\n",
    "# Change edges by modifying the neighbors array\n",
    "g.vertices[3].neighbors[1] = 1           # Change edge, 3->1 instead of 3->4\n",
    "pop!(g.vertices[5].neighbors)            # Remove edge\n",
    "\n",
    "# Add a new vertex by growing the vertices array, and if needed\n",
    "# updating some of the existing neighbors array\n",
    "# Note that the new vertex should be numbered last, otherwise\n",
    "# all node numbers must be reordered\n",
    "newv = Vertex([1,4,9], coordinates=[-.1,.3])\n",
    "push!(g.vertices, newv)\n",
    "push!(g.vertices[2].neighbors, 11)\n",
    "\n",
    "# Deleting a vertex is difficult using adjancency lists\n",
    "# (unless it is the last vertex), since it requires renumbering\n",
    "# of the vertices. But as a compromise, we can remove all edges\n",
    "# to/from a vertex:\n",
    "resize!(g.vertices[8].neighbors,0)\n",
    "for v in g.vertices\n",
    "    filter!(i -> i != 8, v.neighbors)\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.2",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}